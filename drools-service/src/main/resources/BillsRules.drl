import java.util.List
import com.dmarjanovic.drools.domain.*
import scala.collection.JavaConversions
import org.joda.time.DateTime
import scala.Option

rule "Determine if Bill achieves Basic Discount based on Amount"
    agenda-group "bill-bonuses"
    when
        $bill: Bill(amount > 200000)
    then
        $bill.addDiscount(new BillDiscount(5, DiscountType.BASIC));
end

rule "Determine if Bill achieves Pro Discount based on Old Customer"
    agenda-group "bill-bonuses"
    when
        $bill: Bill(customer.registeredAt() < DateTime.now().minusYears(2))
    then
        $bill.addDiscount(new BillDiscount(1, DiscountType.PRO));
end

rule "Determine if Bill achieves Pro Discount based on Customer Category"
    agenda-group "bill-bonuses"
    when
        $bill: Bill(customer.buyerCategory.name.equals("Gold Buyer") || customer.buyerCategory.name.equals("Silver Buyer"))
    then
        $bill.addDiscount(new BillDiscount(1, DiscountType.PRO));
end

rule "Determine if Bill achieves Pro Discount based on Amount"
    agenda-group "bill-bonuses"
    when
        $bill: Bill(amount > 50000)
    then
        final List<BillItem> items = JavaConversions.seqAsJavaList($bill.getSortedItems());
        final int size = (items.size() < 10) ? items.size() : 10;
        int total = 0;
        for (int i = 0; i < size; ++i) {
            total += items.get(i).amount();
        }

        if (total > 0.5 * $bill.amount())
            $bill.addDiscount(new BillDiscount(3, DiscountType.PRO));
end

rule "Determine final Bill discount and price"
    salience -16
    agenda-group "bill-bonuses"
    when
        $bill: Bill()
        $total: Double() from accumulate(BillDiscount($discount: discount) from JavaConversions.seqAsJavaList($bill.discounts), sum($discount))
        $price: Double() from accumulate(BillItem($amount: amount) from JavaConversions.seqAsJavaList($bill.items), sum($amount))
    then
        $bill.discount_$eq($total);
        final double discountAmount = $total * $price / 100.0;
        $bill.amount_$eq($price - discountAmount - $bill.pointsSpent());
        $bill.discountAmount_$eq(discountAmount);
end
